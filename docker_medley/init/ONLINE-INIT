(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 5-Dec-2022 17:34:33" {DSK}<home>frank>il>online>docker_medley>init>ONLINE-INIT.;11 17711  

      :CHANGES-TO (VARS ONLINE-INITCOMS)
                  (FNS Online.CreateButtons Online.CreateLabel Online.SftpInitInfo 
                       Online.SftpUpdateInfo Online.InitNotecards Online.DoInit Online.ActivateCLOS 
                       Online.ActivateRooms Online.ShowDoc)
                  (ADVICE (SAVEVM :IN \IDLER))

      :PREVIOUS-DATE " 3-Dec-2022 19:43:52" 
{DSK}<home>frank>il>online>docker_medley>init>ONLINE-INIT.;9)


(PRETTYCOMPRINT ONLINE-INITCOMS)

(RPAQQ ONLINE-INITCOMS
       [(FILES (SYSLOAD)
               MEDLEYDIR-INIT)
        (GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay 
               IDLE.PROFILE IDLE.BOUNCING.BOX Online.SftpDisplayMenu ONLINEP)
        (INITVARS (Online.LogoutTimeout 30)
               ONLINEP)
        (ADVISE (SAVEVM :IN \IDLER))
        (FNS Online.SftpInitInfo Online.SftpUpdateInfo Online.InitNotecards Online.DoInit 
             Online.CreateButtons Online.CreateLabel Online.ActivateCLOS Online.ActivateRooms 
             XCL-USER::INTERLISP Online.ShowDoc)
        (P (Online.DoInit))
        (DECLARE%: FIRST (P (BKSYSBUF " "])

(FILESLOAD (SYSLOAD)
       MEDLEYDIR-INIT)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay IDLE.PROFILE
       IDLE.BOUNCING.BOX Online.SftpDisplayMenu ONLINEP)
)

(RPAQ? Online.LogoutTimeout 30)

(RPAQ? ONLINEP NIL)

[XCL:REINSTALL-ADVICE '(SAVEVM :IN \IDLER)
       :AROUND
       '((:LAST (COND
                   (ONLINEP (LOGOUT))
                   (T *]

(READVISE (SAVEVM :IN \IDLER))
(DEFINEQ

(Online.SftpInitInfo
  [LAMBDA NIL                                                (* ; "Edited 17-Dec-2021 01:25 by abc")
    (SETQ Online.SftpPort (UNIX-GETENV "SFTP_PORT"))
    (SETQ Online.SftpPassword (UNIX-GETENV "SFTP_PWD"))
    (SETQ Online.SftpDisplay (CONCAT Online.SftpPort " / " (L-CASE USERNAME)
                                    " / " Online.SftpPassword))
    (SETQ Online.SftpDisplayMenu (create MENU
                                        TITLE _ "SFTP Info"
                                        ITEMS _ (LIST (MKATOM (CONCAT "Port: " Online.SftpPort))
                                                      (MKATOM (CONCAT "User: " (L-CASE USERNAME)))
                                                      (MKATOM (CONCAT "Pwd: " Online.SftpPassword)))
                                        MENUFONT _ (FONTCREATE 'CLASSIC 14 'BOLD])

(Online.SftpUpdateInfo
  [LAMBDA (WHEN)                                          (* ; "Edited 23-Nov-2021 22:44 by medley")
    (COND
       ((OR (EQ WHEN 'AFTERLOGOUT)
            (EQ WHEN 'AFTERSAVEVM))
        (Online.SftpInitInfo])

(Online.InitNotecards
  [LAMBDA NIL
    (DECLARE (GLOBALVARS BackgroundMenuCommands BackgroundMenu))
                                                             (* ; "Edited 12-Nov-2022 14:41 by FGH")
                                                             (* ; "Edited 11-Sep-2022 01:09 by fgh")
                                                             (* ; "Edited  7-Feb-2022 20:22 by tp7")
    (PROGN (NC.BringUpNoteCardsIcon (create POSITION
                                           XCOORD _ 750
                                           YCOORD _ (IDIFFERENCE SCREENHEIGHT 228)))
           (NC.FileBrowserMenu NC.NoteCardsIconWindow (PACKFILENAME 'HOST "DSK" 'DIRECTORY
                                                             (CONCAT (UNIX-GETENV 'NOTEFILESDIR))
                                                             'NAME "*" 'EXTENSION "notefile")
                  (CREATEREGION 50 (IDIFFERENCE SCREENHEIGHT 700)
                         550 220))
           (if (NULL (SASSOC 'NoteCards BackgroundMenuCommands))
               then (PROGN [SETQ BackgroundMenuCommands (APPEND BackgroundMenuCommands
                                                               (LIST '(NoteCards (
                                                                              NC.BringUpNoteCardsIcon
                                                                                  )
                                                                             
                                                               "Bring up the NoteCards control icon."
                                                                             ]
                           (SETQ BackgroundMenu NIL])

(Online.DoInit
  [LAMBDA NIL

    (* ;; "Edited 12-Nov-2022 13:57 by FGH")

    (* ;; "Edited 12-Oct-2022 20:23 by fgh")

    (* ;; "Edited  6-Sep-2022 17:22 by fgh")

    (* ;; "Edited  4-Sep-2022 16:44 by larry")

    (* ;; "Edited 18-Mar-2022 18:53 by fgh")

    (* ;; "Edited 17-Dec-2021 22:05 by fgh")

    (PROGN 
           (* ;; "Set up SFTP Info widget in WHO-LINE window")

           (Online.SftpInitInfo)
           (NCONC1 AROUNDEXITFNS 'Online.SftpUpdateInfo)
           [COND
              ([NOTANY *WHO-LINE-ENTRY-REGISTRY* (FUNCTION (LAMBDA (ENTRY)
                                                             (STRING-EQUAL (CAR ENTRY)
                                                                    "SFTP"]
               (PROGN (NCONC1 *WHO-LINE-ENTRIES* (LIST "SFTP" 'Online.SftpDisplay 30
                                                       [FUNCTION (LAMBDA NIL
                                                                   (MENU Online.SftpDisplayMenu]
                                                       NIL 
                                       "Port/Username/Password thru which to access files using SFTP"
                                                       ))
                      (NCONC *WHO-LINE-ENTRY-REGISTRY* (LAST *WHO-LINE-ENTRIES*))
                      (INSTALL-WHO-LINE-OPTIONS]

           (* ;; "  Set the IDLE timeout, set to SAVEVM 1 min after idle, set idle bouncing box")

           (LISTPUT IDLE.PROFILE 'SAVEVM 1)
           (LISTPUT IDLE.PROFILE 'TIMEOUT Online.LogoutTimeout)
           (SETQ IDLE.BOUNCING.BOX "Press any key to continue")

           (* ;; " Adjust windows so that the exec window and the prompt window don't overlap")

           [MAPC (OPENWINDOWS)
                 (FUNCTION (LAMBDA (W)
                             (COND
                                ((EQ (WINDOWPROP W 'BUTTONEVENTFN)
                                     'WHEN-WHO-LINE-SELECTED-FN)
                                 (MOVEW W (CAR (WINDOWPROP W 'REGION))
                                        (IDIFFERENCE SCREENHEIGHT 18)))
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Prompt Window")
                                 (PROGN (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 120)))
                                        (CLEARW W)))
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Exec  (XCL)")
                                 (PROGN (WINDOWPROP W 'TITLE "Exec  (INTERLISP)")
                                        (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 460]

           (* ;; " Set up INITIALSLST based on information passed in from the Linux environment")

           [SETQ INITIALSLST (LIST (LIST USERNAME (UNIX-GETENV 'MEDLEY_FIRSTNAME)
                                         (UNIX-GETENV 'MEDLEY_INITIALS]

           (* ;; " load and initialize Notecards if requested")

           (COND
              ((STRING-EQUAL (UNIX-GETENV 'RUN_NOTECARDS)
                      "true")
               (Online.InitNotecards)))

           (* ;; "set ONLINEP as a definitive flag that this is an online session")

           (COND
              ((STRING-EQUAL (UNIX-GETENV 'MEDLEY_ONLINE)
                      "true")
               (SETTOPVAL 'ONLINEP T)))

           (* ;; 
     "set the opener for accesssing the Common Lisp Hyperspec - for use in HELPSYS LispUsers package")

           (SETQ CLHS.OPENER "/usr/local/interlisp/online/bin/request_new_tab")

           (* ;; "Temp 2022-10-12 to get latest HELPSYS into online without a release")

           (LOAD '{DSK}/usr/local/interlisp/medley/lispusers/HELPSYS.LCOM T)

           (* ;; "change to interlisp exec if required")

           (COND
              ((OR (STRING-EQUAL (UNIX-GETENV 'MEDLEY_EXEC)
                          "inter")
                   (STRING-EQUAL (UNIX-GETENV 'NCO)
                          "true"))
               (BKSYSBUF "(INTERLISP)")))

           (* ;; " create the ROOMS, Notecards and CLOS Activation Buttons")

           (Online.CreateButtons])

(Online.CreateButtons
  [LAMBDA NIL                                                (* ; "Edited  5-Dec-2022 17:31 by FGH")
                                                             (* ; "Edited 12-Nov-2022 14:52 by FGH")

    (* ;; " Create activation buttons for Rooms, Notecards and CLOS")

    (LET* [(RIGHTMARGINISH 140)
           (SECTION1YPOS 250)
           (YPOSDELTA 65)
           (SECTION2YPOS (IPLUS SECTION1YPOS (ITIMES YPOSDELTA 3)))
           [IWS (LIST (Online.CreateLabel "DOCUMENTATION" (IDIFFERENCE SCREENWIDTH (IDIFFERENCE
                                                                                    RIGHTMARGINISH 50
                                                                                    ))
                             (IDIFFERENCE SCREENHEIGHT SECTION1YPOS))
                      (Online.CreateLabel "ACTIVATE" (IDIFFERENCE SCREENWIDTH (IDIFFERENCE 
                                                                                     RIGHTMARGINISH 
                                                                                     50))
                             (IDIFFERENCE SCREENHEIGHT (IDIFFERENCE SECTION2YPOS 20)))
                      (Online.CreateLabel "FEATURES" (IDIFFERENCE SCREENWIDTH (IDIFFERENCE 
                                                                                     RIGHTMARGINISH 
                                                                                     50))
                             (IDIFFERENCE SCREENHEIGHT SECTION2YPOS]
           (BUTTONS (LIST [CREATE-BUTTON '(Online.ShowDoc T)
                                 "MANUAL"
                                 (create POSITION
                                        XCOORD _ (IDIFFERENCE SCREENWIDTH RIGHTMARGINISH)
                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT (IPLUS SECTION1YPOS
                                                                                  (ITIMES 1 YPOSDELTA
                                                                                         ]
                          [CREATE-BUTTON '(Online.ShowDoc NIL)
                                 "PRIMER"
                                 (create POSITION
                                        XCOORD _ (IDIFFERENCE SCREENWIDTH RIGHTMARGINISH)
                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT (IPLUS SECTION1YPOS
                                                                                  (ITIMES 2 YPOSDELTA
                                                                                         ]
                          [CREATE-BUTTON '(Online.InitNotecards)
                                 "NOTECARDS"
                                 (create POSITION
                                        XCOORD _ (IDIFFERENCE SCREENWIDTH RIGHTMARGINISH)
                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT (IPLUS SECTION2YPOS
                                                                                  (ITIMES 1 YPOSDELTA
                                                                                         ]
                          [CREATE-BUTTON '(Online.ActivateRooms)
                                 "ROOMS"
                                 (create POSITION
                                        XCOORD _ (IDIFFERENCE SCREENWIDTH RIGHTMARGINISH)
                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT (IPLUS SECTION2YPOS
                                                                                  (ITIMES 2 YPOSDELTA
                                                                                         ]
                          (CREATE-BUTTON '(Online.ActivateCLOS)
                                 "CLOS"
                                 (create POSITION
                                        XCOORD _ (IDIFFERENCE SCREENWIDTH RIGHTMARGINISH)
                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT (IPLUS SECTION2YPOS
                                                                                  (ITIMES 3 YPOSDELTA
                                                                                         ]
          [for B in BUTTONS do (WINDOWPROP B 'RIGHTBUTTONFN 'NILL)
                               (WINDOWPROP B 'BUTTONEVENTFN (FUNCTION (LAMBDA (BUTTON)
                                                                        (if (LASTMOUSESTATE
                                                                             (ONLY LEFT))
                                                                            then (EXECUTE-BUTTON
                                                                                  BUTTON]
          (for IW in IWS do (WINDOWPROP IW 'RIGHTBUTTONFN 'NILL))
          IWS])

(Online.CreateLabel
  [LAMBDA (Text CenterX BottomY)                             (* ; "Edited  5-Dec-2022 16:49 by FGH")
    (LET* ((DS (DSPCREATE))
           (FONT (DSPFONT '(HELVETICA 18 BOLD)
                        DS))
           (SR (STRINGREGION Text DS))
           (BMW (fetch (REGION WIDTH) of SR))
           (BMH (IPLUS (fetch (REGION HEIGHT) of SR)
                       (fetch (REGION BOTTOM) of SR)))
           (BM (BITMAPCREATE BMW BMH))
           (POS (create POSITION
                       XCOORD _ (IDIFFERENCE CenterX (IQUOTIENT BMW 2))
                       YCOORD _ BottomY)))
          (DSPDESTINATION BM DS)
          (PRIN1 Text DS)
          (ICONW BM BM POS])

(Online.ActivateCLOS
  [LAMBDA NIL
    (DECLARE (GLOBALVARS BackgroundMenuCommands BackgroundMenu))
                                                             (* ; "Edited 12-Nov-2022 14:41 by FGH")
    (if (NULL (SASSOC "CLOS Browse Class" BackgroundMenuCommands))
        then (PROGN [SETQ BackgroundMenuCommands
                     (APPEND BackgroundMenuCommands
                            (LIST '("CLOS Browse Class" (CLOS-BROWSER::BROWSE-CLASS)
                                          "Bring up a class browser."
                                          (SUBITEMS (|all in a package| (CLOS-BROWSER::BROWSE-CLASS
                                                                         (
                                                                     CLOS-BROWSER::CLASSES-IN-PACKAGE
                                                                          (
                                                                      CLOS-BROWSER::IN-SELECT-PACKAGE
                                                                           )))
                                                           
                               "Select a package and browse all the classes defined in that package."
                                                           ]
                    (SETQ BackgroundMenu NIL])

(Online.ActivateRooms
  [LAMBDA NIL
    (DECLARE (GLOBALVARS BackgroundMenuCommands))            (* ; "Edited 12-Nov-2022 14:56 by FGH")
    (if (NULL (SASSOC "Rooms" BackgroundMenuCommands))
        then (ROOMS:RESET])

(XCL-USER::INTERLISP
  [LAMBDA NIL                                                (* ; "Edited 18-Mar-2022 18:53 by fgh")
    (PROGN [MAPC (OPENWINDOWS)
                 (FUNCTION (LAMBDA (W)
                             (COND
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Exec  (XCL)")
                                 (PROGN (WINDOWPROP W 'TITLE "Exec  (INTERLISP)")
                                        (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 460]
           (XCL:SET-DEFAULT-EXEC-TYPE 'INTERLISP)
           (XCL:SET-EXEC-TYPE 'INTERLISP])

(Online.ShowDoc
  [LAMBDA (IRMP)
    (ShellCommand (CONCAT "/usr/local/interlisp/online/bin/request_new_tab" " "
                         (if IRMP
                             then "https://interlisp.org/documentation/IRM.pdf"
                           else "https://interlisp.org/documentation/Medley-Primer.pdf")
                         " > /tmp/showdoc-warnings-$$.txt 2>&1"])
)

(Online.DoInit)
(DECLARE%: FIRST 

(BKSYSBUF " ")
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1816 17631 (Online.SftpInitInfo 1826 . 2699) (Online.SftpUpdateInfo 2701 . 2956) (
Online.InitNotecards 2958 . 4717) (Online.DoInit 4719 . 9218) (Online.CreateButtons 9220 . 14129) (
Online.CreateLabel 14131 . 14863) (Online.ActivateCLOS 14865 . 16216) (Online.ActivateRooms 16218 . 
16461) (XCL-USER::INTERLISP 16463 . 17230) (Online.ShowDoc 17232 . 17629)))))
STOP
