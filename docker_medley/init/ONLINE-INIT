(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "11-Oct-2022 22:17:35" {DSK}<home>medley>il>ONLINE-INIT.;2 8836   

      :CHANGES-TO (FNS Online.DoInit)

      :PREVIOUS-DATE "11-Sep-2022 01:14:05" {DSK}<home>medley>il>ONLINE-INIT.;1)


(PRETTYCOMPRINT ONLINE-INITCOMS)

(RPAQQ ONLINE-INITCOMS
       [(FILES (SYSLOAD)
               MEDLEYDIR-INIT VTCHAT)
        (GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay 
               IDLE.PROFILE IDLE.BOUNCING.BOX Online.SftpDisplayMenu ONLINEP)
        (INITVARS (Online.LogoutTimeout 30)
               ONLINEP)
        (ADVISE (SAVEVM :IN \IDLER))
        (FNS Online.SftpInitInfo Online.SftpUpdateInfo Online.InitNotecards Online.DoInit 
             XCL-USER::INTERLISP)
        (P (Online.DoInit))
        (DECLARE%: FIRST (P (BKSYSBUF " "])

(FILESLOAD (SYSLOAD)
       MEDLEYDIR-INIT VTCHAT)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay IDLE.PROFILE
       IDLE.BOUNCING.BOX Online.SftpDisplayMenu ONLINEP)
)

(RPAQ? Online.LogoutTimeout 30)

(RPAQ? ONLINEP NIL)

[XCL:REINSTALL-ADVICE '(SAVEVM :IN \IDLER)
       :AROUND
       '((:LAST (COND
                   (ONLINEP (LOGOUT))
                   (T *]

(READVISE (SAVEVM :IN \IDLER))
(DEFINEQ

(Online.SftpInitInfo
  [LAMBDA NIL                                                (* ; "Edited 17-Dec-2021 01:25 by abc")
    (SETQ Online.SftpPort (UNIX-GETENV "SFTP_PORT"))
    (SETQ Online.SftpPassword (UNIX-GETENV "SFTP_PWD"))
    (SETQ Online.SftpDisplay (CONCAT Online.SftpPort " / " (L-CASE USERNAME)
                                    " / " Online.SftpPassword))
    (SETQ Online.SftpDisplayMenu (create MENU
                                        TITLE _ "SFTP Info"
                                        ITEMS _ (LIST (MKATOM (CONCAT "Port: " Online.SftpPort))
                                                      (MKATOM (CONCAT "User: " (L-CASE USERNAME)))
                                                      (MKATOM (CONCAT "Pwd: " Online.SftpPassword)))
                                        MENUFONT _ (FONTCREATE 'CLASSIC 14 'BOLD])

(Online.SftpUpdateInfo
  [LAMBDA (WHEN)                                          (* ; "Edited 23-Nov-2021 22:44 by medley")
    (COND
       ((OR (EQ WHEN 'AFTERLOGOUT)
            (EQ WHEN 'AFTERSAVEVM))
        (Online.SftpInitInfo])

(Online.InitNotecards
  [LAMBDA NIL                                                (* ; "Edited 11-Sep-2022 01:09 by fgh")
                                                             (* ; "Edited  7-Feb-2022 20:22 by tp7")
    (PROGN (LOAD? (PACKFILENAME 'HOST "DSK" 'DIRECTORY (CONCAT (UNIX-GETENV 'NC_INSTALLDIR)
                                                              "/system")
                         'NAME "NOTECARDS" 'EXTENSION "LCOM"))
           (ADVISE 'NC.LogoutAdvice 'BEFORE 'FIRST '(RETURN T))
           (NC.BringUpNoteCardsIcon (create POSITION
                                           XCOORD _ 750
                                           YCOORD _ (IDIFFERENCE SCREENHEIGHT 228)))
           (NC.FileBrowserMenu NC.NoteCardsIconWindow (PACKFILENAME 'HOST "DSK" 'DIRECTORY
                                                             (CONCAT (UNIX-GETENV 'NOTEFILESDIR))
                                                             'NAME "*" 'EXTENSION "notefile")
                  (CREATEREGION 50 (IDIFFERENCE SCREENHEIGHT 700)
                         550 220])

(Online.DoInit
  [LAMBDA NIL

    (* ;; "Edited 11-Oct-2022 22:16 by fgh")

    (* ;; "Edited  6-Sep-2022 17:22 by fgh")

    (* ;; "Edited  4-Sep-2022 16:44 by larry")

    (* ;; "Edited 18-Mar-2022 18:53 by fgh")

    (* ;; "Edited 17-Dec-2021 22:05 by fgh")

    (PROGN 
           (* ;; "Set up SFTP Info widget in WHO-LINE window")

           (Online.SftpInitInfo)
           (NCONC1 AROUNDEXITFNS 'Online.SftpUpdateInfo)
           [COND
              ([NOTANY *WHO-LINE-ENTRY-REGISTRY* (FUNCTION (LAMBDA (ENTRY)
                                                             (STRING-EQUAL (CAR ENTRY)
                                                                    "SFTP"]
               (PROGN (NCONC1 *WHO-LINE-ENTRIES* (LIST "SFTP" 'Online.SftpDisplay 30
                                                       [FUNCTION (LAMBDA NIL
                                                                   (MENU Online.SftpDisplayMenu]
                                                       NIL 
                                       "Port/Username/Password thru which to access files using SFTP"
                                                       ))
                      (NCONC *WHO-LINE-ENTRY-REGISTRY* (LAST *WHO-LINE-ENTRIES*))
                      (INSTALL-WHO-LINE-OPTIONS]

           (* ;; "  Set the IDLE timeout, set to SAVEVM 1 min after idle, set idle bouncing box")

           (LISTPUT IDLE.PROFILE 'SAVEVM 1)
           (LISTPUT IDLE.PROFILE 'TIMEOUT Online.LogoutTimeout)
           (SETQ IDLE.BOUNCING.BOX "Press any key to continue")

           (* ;; " Adjust windows so that the exec window and the prompt window don't overlap")

           [MAPC (OPENWINDOWS)
                 (FUNCTION (LAMBDA (W)
                             (COND
                                ((EQ (WINDOWPROP W 'BUTTONEVENTFN)
                                     'WHEN-WHO-LINE-SELECTED-FN)
                                 (MOVEW W (CAR (WINDOWPROP W 'REGION))
                                        (IDIFFERENCE SCREENHEIGHT 18)))
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Prompt Window")
                                 (PROGN (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 120)))
                                        (CLEARW W)))
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Exec  (XCL)")
                                 (PROGN (WINDOWPROP W 'TITLE "Exec  (INTERLISP)")
                                        (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 460]

           (* ;; 
           "Remove default CHAT display type so that VT100 can become the default CHAT display type")

           (RPLACA (CAR CHAT.DISPLAYTYPES)
                  NIL)

           (* ;; " Set up INITIALSLST based on information passed in from the Linux environment")

           [SETQ INITIALSLST (LIST (LIST USERNAME (UNIX-GETENV 'MEDLEY_FIRSTNAME)
                                         (UNIX-GETENV 'MEDLEY_INITIALS]

           (* ;; " load and initialize Notecards if requested")

           (COND
              ((STRING-EQUAL (UNIX-GETENV 'RUN_NOTECARDS)
                      "true")
               (Online.InitNotecards)))

           (* ;; "set ONLINEP as a definitive flag that this is an online session")

           (COND
              ((STRING-EQUAL (UNIX-GETENV 'MEDLEY_ONLINE)
                      "true")
               (SETTOPVAL 'ONLINEP T)))

           (* ;; 
     "set the opener for accesssing the Common Lisp Hyperspec - for use in HELPSYS LispUsers package")

           (SETQ CLHS.OPENER "/usr/local/interlisp/online/bin/request_new_tab")

           (* ;; "change to interlisp exec if required")

           (COND
              ((OR (STRING-EQUAL (UNIX-GETENV 'MEDLEY_EXEC)
                          "inter")
                   (STRING-EQUAL (UNIX-GETENV 'NCO)
                          "true"))
               (BKSYSBUF "(INTERLISP)"])

(XCL-USER::INTERLISP
  [LAMBDA NIL                                                (* ; "Edited 18-Mar-2022 18:53 by fgh")
    (PROGN [MAPC (OPENWINDOWS)
                 (FUNCTION (LAMBDA (W)
                             (COND
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Exec  (XCL)")
                                 (PROGN (WINDOWPROP W 'TITLE "Exec  (INTERLISP)")
                                        (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 460]
           (XCL:SET-DEFAULT-EXEC-TYPE 'INTERLISP)
           (XCL:SET-EXEC-TYPE 'INTERLISP])
)

(Online.DoInit)
(DECLARE%: FIRST 

(BKSYSBUF " ")
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1373 8756 (Online.SftpInitInfo 1383 . 2256) (Online.SftpUpdateInfo 2258 . 2513) (
Online.InitNotecards 2515 . 3632) (Online.DoInit 3634 . 7985) (XCL-USER::INTERLISP 7987 . 8754)))))
STOP
