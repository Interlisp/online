(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "23-Aug-2022 13:16:27" {DSK}<home>larry>online>docker_medley>init>ONLINE-INIT.;2 8698   

      :CHANGES-TO (VARS ONLINE-INITCOMS)
                  (ADVICE IDLE)
                  (FNS Online.SftpInitInfo Online.SftpUpdateInfo Online.InitNotecards Online.DoInit 
                       XCL-USER::INTERLISP)

      :PREVIOUS-DATE "18-Mar-2022 18:54:20" {DSK}<home>larry>online>docker_medley>init>ONLINE-INIT.;1
)


(PRETTYCOMPRINT ONLINE-INITCOMS)

(RPAQQ ONLINE-INITCOMS
       [(FILES (SYSLOAD)
               MEDLEYDIR-INIT VTCHAT)
        (GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay 
               IDLE.PROFILE Online.SftpDisplayMenu ONLINEP)
        (INITVARS (Online.LogoutTimeout 30)
               ONLINEP)
        (ADVISE IDLE)
        (FNS Online.SftpInitInfo Online.SftpUpdateInfo Online.InitNotecards Online.DoInit 
             XCL-USER::INTERLISP)
        (P (Online.DoInit))
        (DECLARE%: FIRST (P (BKSYSBUF " "])

(FILESLOAD (SYSLOAD)
       MEDLEYDIR-INIT VTCHAT)
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS Online.LogoutTimeout Online.SftpPort Online.SftpPassword Online.SftpDisplay IDLE.PROFILE
       Online.SftpDisplayMenu ONLINEP)
)

(RPAQ? Online.LogoutTimeout 30)

(RPAQ? ONLINEP NIL)

[XCL:REINSTALL-ADVICE 'IDLE :BEFORE '((:LAST (if (IGREATERP (IQUOTIENT (IDIFFERENCE (CLOCK 0)
                                                                              (CLOCK 1))
                                                                   1000)
                                                        30)
                                                 then (LOGOUT]

(READVISE IDLE)
(DEFINEQ

(Online.SftpInitInfo
  [LAMBDA NIL                                                (* ; "Edited 17-Dec-2021 01:25 by abc")
    (SETQ Online.SftpPort (UNIX-GETENV "SFTP_PORT"))
    (SETQ Online.SftpPassword (UNIX-GETENV "SFTP_PWD"))
    (SETQ Online.SftpDisplay (CONCAT Online.SftpPort " / " (L-CASE USERNAME)
                                    " / " Online.SftpPassword))
    (SETQ Online.SftpDisplayMenu (create MENU
                                        TITLE _ "SFTP Info"
                                        ITEMS _ (LIST (MKATOM (CONCAT "Port: " Online.SftpPort))
                                                      (MKATOM (CONCAT "User: " (L-CASE USERNAME)))
                                                      (MKATOM (CONCAT "Pwd: " Online.SftpPassword)))
                                        MENUFONT _ (FONTCREATE 'CLASSIC 14 'BOLD])

(Online.SftpUpdateInfo
  [LAMBDA (WHEN)                                          (* ; "Edited 23-Nov-2021 22:44 by medley")
    (COND
       ((OR (EQ WHEN 'AFTERLOGOUT)
            (EQ WHEN 'AFTERSAVEVM))
        (Online.SftpInitInfo])

(Online.InitNotecards
  [LAMBDA NIL                                                (* ; "Edited  7-Feb-2022 20:22 by tp7")
    (PROGN (LOAD? (PACKFILENAME 'HOST "DSK" 'DIRECTORY (CONCAT (UNIX-GETENV 'NC_INSTALLDIR)
                                                              "/system")
                         'NAME "NOTECARDS" 'EXTENSION "LCOM"))
           (NC.BringUpNoteCardsIcon (create POSITION
                                           XCOORD _ 750
                                           YCOORD _ (IDIFFERENCE SCREENHEIGHT 228)))
           (NC.FileBrowserMenu NC.NoteCardsIconWindow (PACKFILENAME 'HOST "DSK" 'DIRECTORY
                                                             (CONCAT (UNIX-GETENV 'NOTEFILESDIR))
                                                             'NAME "*" 'EXTENSION "notefile")
                  (CREATEREGION 50 (IDIFFERENCE SCREENHEIGHT 700)
                         550 220])

(Online.DoInit
  [LAMBDA NIL                                                (* ; "Edited 18-Mar-2022 18:53 by fgh")

    (* ;; "Edited 17-Dec-2021 22:05 by fgh")

    (PROGN 
           (* ;; "Set up SFTP Info widget in WHO-LINE window")

           (Online.SftpInitInfo)
           (CL:UNLESS (FMEMB 'Online.SftpUpdateInfo AROUNDEXITFNS)
               (NCONC1 AROUNDEXITFNS 'Online.SftpUpdateInfo))
           [COND
              ([NOTANY *WHO-LINE-ENTRY-REGISTRY* (FUNCTION (LAMBDA (ENTRY)
                                                             (STRING-EQUAL (CAR ENTRY)
                                                                    "SFTP"]
               (PROGN (NCONC1 *WHO-LINE-ENTRIES* (LIST "SFTP" 'Online.SftpDisplay 30
                                                       [FUNCTION (LAMBDA NIL
                                                                   (MENU Online.SftpDisplayMenu]
                                                       NIL 
                                       "Port/Username/Password thru which to access files using SFTP"
                                                       ))
                      (NCONC *WHO-LINE-ENTRY-REGISTRY* (LAST *WHO-LINE-ENTRIES*))
                      (INSTALL-WHO-LINE-OPTIONS]

           (* ;; "  Set the IDLE timeout ")

           (LISTPUT IDLE.PROFILE 'TIMEOUT Online.LogoutTimeout)
           (LISTPUT IDLE.PROFILE 'SAVEVM 1)

           (* ;; " Adjust windows so that the exec window and the prompt window don't overlap")

           [MAPC (OPENWINDOWS)
                 (FUNCTION (LAMBDA (W)
                             (COND
                                ((EQ (WINDOWPROP W 'BUTTONEVENTFN)
                                     'WHEN-WHO-LINE-SELECTED-FN)
                                 (MOVEW W (CAR (WINDOWPROP W 'REGION))
                                        (IDIFFERENCE SCREENHEIGHT 18)))
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Prompt Window")
                                 (PROGN (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 120)))
                                        (CLEARW W)))
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Exec  (XCL)")
                                 (PROGN (WINDOWPROP W 'TITLE "Exec  (INTERLISP)")
                                        (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 460]

           (* ;; 
           "Remove default CHAT display type so that VT100 can become the default CHAT display type")

           (RPLACA (CAR CHAT.DISPLAYTYPES)
                  NIL)

           (* ;; " Set up INITIALSLST based on information passed in from the Linux environment")

           [SETQ INITIALSLST (LIST (LIST USERNAME (UNIX-GETENV 'MEDLEY_FIRSTNAME)
                                         (UNIX-GETENV 'MEDLEY_INITIALS]

           (* ;; " load and initialize Notecards if requested")

           (COND
              ((STRING-EQUAL (UNIX-GETENV 'RUN_NOTECARDS)
                      "true")
               (Online.InitNotecards)))

           (* ;; "set ONLINEP as a definitive flag that this is an online session")

           (COND
              ((STRING-EQUAL (UNIX-GETENV 'MEDLEY_ONLINE)
                      "true")
               (SETTOPVAL 'ONLINEP T)))

           (* ;; "change to interlisp exec if required")

           (COND
              ((OR (STRING-EQUAL (UNIX-GETENV 'MEDLEY_EXEC)
                          "inter")
                   (STRING-EQUAL (UNIX-GETENV 'NCO)
                          "true"))
               (BKSYSBUF "(INTERLISP)"])

(XCL-USER::INTERLISP
  [LAMBDA NIL                                                (* ; "Edited 18-Mar-2022 18:53 by fgh")
    (PROGN [MAPC (OPENWINDOWS)
                 (FUNCTION (LAMBDA (W)
                             (COND
                                ((STREQUAL (WINDOWPROP W 'TITLE)
                                        "Exec  (XCL)")
                                 (PROGN (WINDOWPROP W 'TITLE "Exec  (INTERLISP)")
                                        (MOVEW W (create POSITION
                                                        XCOORD _ 50
                                                        YCOORD _ (IDIFFERENCE SCREENHEIGHT 460]
           (XCL:SET-DEFAULT-EXEC-TYPE 'INTERLISP)
           (XCL:SET-EXEC-TYPE 'INTERLISP])
)

(Online.DoInit)
(DECLARE%: FIRST 

(BKSYSBUF " ")
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1772 8618 (Online.SftpInitInfo 1782 . 2655) (Online.SftpUpdateInfo 2657 . 2912) (
Online.InitNotecards 2914 . 3858) (Online.DoInit 3860 . 7847) (XCL-USER::INTERLISP 7849 . 8616)))))
STOP
